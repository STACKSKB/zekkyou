defmodule Zekkyou.Tools.ApplyWorkerPatch do
  @moduledoc "Apply a descendant worker's captured patch through Alto's prepared approval contract."
  @behaviour Alto.Tool

  def name, do: :apply_worker_patch
  def execution_mode, do: :exclusive
  def approval, do: :required

  def schema do
    %{
      description:
        "Apply a reviewed worker patch to this team's source checkout. " <>
          "Use the workspace ID and revision from the worker result. Approval binds the " <>
          "exact patch and affected files; changed files require a new review. " <>
          "Inspect the patch with review_worker_patch before requesting application.",
      parameters: %{
        type: "object",
        properties: %{
          workspace_id: %{type: "string"},
          revision: %{type: "integer", minimum: 0}
        },
        required: ["workspace_id", "revision"],
        additionalProperties: false
      }
    }
  end

  def prepare(arguments, context), do: prepare(arguments, context, [])

  def prepare(%{"workspace_id" => id, "revision" => revision} = args, context, opts)
      when map_size(args) == 2 do
    with {:ok, manager, info} <- Zekkyou.Workspaces.for_descendant(context, id, opts),
         {:ok, prepared} <- Alto.Workspaces.prepare_apply(manager, id, revision),
         {:ok, patch} <- Alto.Workspaces.patch(manager, id) do
      details = %{
        workspace_id: id,
        revision: revision,
        source: info.workspace["source"],
        sha256: prepared["patch_sha256"],
        bytes: byte_size(patch),
        files: affected_files(prepared["integration"]),
        preview: Zekkyou.Workspaces.patch_preview(patch)
      }

      {:ok, prepared, details}
    end
  end

  def prepare(_, _, _), do: {:error, :invalid_patch_arguments}

  # Backend-specific file lists are optional display data. The manager-level
  # source, revision and digest remain the authority for approval/application.
  defp affected_files(%{"files" => files}) when is_list(files),
    do: for(%{"path" => path} <- files, is_binary(path), do: path)

  defp affected_files(_), do: []

  def run_prepared(prepared, context), do: run_prepared(prepared, context, [])

  def run_prepared(%{"workspace_id" => id} = prepared, context, opts) do
    with {:ok, manager, _} <- Zekkyou.Workspaces.for_descendant(context, id, opts),
         {:ok, info} <- Alto.Workspaces.apply(manager, prepared) do
      {:ok, %{workspace_id: info.id, revision: info.revision, status: info.status}}
    end
  end

  def run_prepared(_, _, _), do: {:error, :invalid_prepared_patch}

  def run(arguments, context), do: run(arguments, context, [])

  def run(arguments, context, opts) do
    with {:ok, prepared, _} <- prepare(arguments, context, opts),
         do: run_prepared(prepared, context, opts)
  end
end

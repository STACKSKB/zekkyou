defmodule Zekkyou.CLI do
  @moduledoc "Command-line access to the resident service."

  alias Zekkyou.{Client, Config, Service}

  @help """
  Zekkyou — persistent agents built on Alto

  zekkyou serve CONFIG.exs
  zekkyou status [--socket PATH]
  zekkyou start PROFILE TASK [--socket PATH]
  zekkyou follow SESSION PROFILE TASK [--socket PATH]
  zekkyou watch RUN [--from-seq N] [--socket PATH]
  zekkyou history SESSION [--cursor N] [--socket PATH]
  zekkyou cancel RUN [--socket PATH]
  zekkyou approve REQUEST [--socket PATH]
  zekkyou deny REQUEST [--socket PATH]
  zekkyou schedule PROFILE TASK [--delay-ms N] [--id KEY] [--socket PATH]
  zekkyou tasks [--socket PATH]
  zekkyou task ID [--socket PATH]
  zekkyou task-cancel ID [--socket PATH]
  zekkyou task-reconcile ID committed|failed|retry --revision N --note TEXT [--socket PATH]
  zekkyou task-decide ID approve|deny --revision N [--socket PATH]
  zekkyou task-child-decide TASK CHILD approve|deny --revision TASK_REV --generation BATCH_GEN --batch-revision BATCH_REV --attempt ATTEMPT --suspension NONCE --key BATCH_KEY [--socket PATH]
  zekkyou task-cleanup TASK --revision TASK_REV [--socket PATH]
  zekkyou task-recover ID KEY --revision TASK_REV --generation G --continuation-revision CELL_REV [--socket PATH]
  zekkyou mailbox-compact [--socket PATH]
  zekkyou mailbox ROOT_RUN_ID [--cursor N] [--socket PATH]
  zekkyou mailbox-get ROOT_RUN_ID MESSAGE_KEY [--socket PATH]
  zekkyou mailbox-cancel ROOT_RUN_ID MESSAGE_KEY [--socket PATH]
  zekkyou workspaces [--cursor N] [--socket PATH]
  zekkyou workspace ID [--socket PATH]
  zekkyou workspace-patch ID [--cursor N] [--socket PATH]
  zekkyou workspace-discard ID --revision N --note TEXT [--socket PATH]
  zekkyou team-batches [--cursor N] [--socket PATH]
  zekkyou team-batch KEY [--socket PATH]
  zekkyou team-result KEY CHILD --generation G --revision N [--cursor N] [--socket PATH]
  zekkyou team-child-approval KEY CHILD --generation G --revision N [--socket PATH]

  Serve owns execution. Closing status/watch clients does not stop agents.
  Configuration is trusted Elixir code. Use SSH socket forwarding for remote access.
  """

  def main(args) do
    case run(args) do
      :ok ->
        :ok

      {:error, reason} ->
        IO.puts(:stderr, "Zekkyou: #{inspect(reason)}")
        System.halt(1)
    end
  end

  def run(args) do
    {opts, args, invalid} =
      OptionParser.parse(
        args,
        strict: [
          socket: :string,
          from_seq: :integer,
          cursor: :integer,
          delay_ms: :integer,
          id: :string,
          revision: :integer,
          continuation_revision: :integer,
          batch_revision: :integer,
          note: :string,
          generation: :string,
          attempt: :string,
          suspension: :string,
          key: :string
        ]
      )

    if invalid == [] do
      dispatch(args, opts)
    else
      {:error, {:invalid_options, invalid}}
    end
  end

  defp dispatch([], _opts), do: IO.puts(@help)
  defp dispatch(["help"], _opts), do: IO.puts(@help)

  defp dispatch(["serve", path], _opts) do
    with {:ok, config} <- Config.load(path),
         {:ok, pid} <- Service.start(config) do
      IO.puts("Zekkyou is listening at #{Config.socket_path(config)}")
      monitor = Process.monitor(pid)

      receive do
        {:DOWN, ^monitor, :process, ^pid, reason} -> {:error, {:service_stopped, reason}}
      end
    end
  end

  defp dispatch(["watch", run], opts) do
    with {:ok, client} <- connect(opts) do
      try do
        with {:ok, _} <-
               Client.request(client, %{
                 "type" => "attach",
                 "run_id" => run,
                 "from_seq" => Keyword.get(opts, :from_seq, 1)
               }),
             do: watch(client)
      after
        Client.close(client)
      end
    end
  end

  defp dispatch(args, opts) do
    with {:ok, command} <- command(args, opts),
         {:ok, client} <- connect(opts) do
      try do
        case Client.request(client, command) do
          {:ok, reply} -> print(reply)
          {:error, reason} -> {:error, reason}
        end
      after
        Client.close(client)
      end
    end
  end

  defp command(["status"], _), do: {:ok, %{"type" => "sessions"}}

  defp command(["start", profile, task], _),
    do: {:ok, %{"type" => "start_run", "config" => profile, "task" => task}}

  defp command(["follow", session, profile, task], _),
    do: {:ok, %{"type" => "start_run", "config" => profile, "task" => task, "resume" => session}}

  defp command(["history", session], opts),
    do:
      {:ok,
       %{
         "type" => "session_events",
         "session_id" => session,
         "cursor" => Keyword.get(opts, :cursor, 0)
       }}

  defp command(["cancel", run], _),
    do: {:ok, %{"type" => "cancel", "run_id" => run, "reason" => "user"}}

  defp command(["schedule", profile, task], opts) do
    payload = %{"profile" => profile, "task" => task}
    payload = maybe_put(payload, "id", Keyword.get(opts, :id))
    payload = maybe_put(payload, "delay_ms", Keyword.get(opts, :delay_ms))
    {:ok, command_wire("tasks.submit", payload)}
  end

  defp command(["tasks"], _opts), do: {:ok, command_wire("tasks.list", %{})}

  defp command(["workspaces"], opts),
    do: {:ok, command_wire("workspaces.list", %{"cursor" => Keyword.get(opts, :cursor, 0)})}

  defp command(["workspace", id], _opts),
    do: {:ok, command_wire("workspaces.get", %{"id" => id})}

  defp command(["workspace-patch", id], opts),
    do:
      {:ok,
       command_wire("workspaces.patch", %{"id" => id, "cursor" => Keyword.get(opts, :cursor, 0)})}

  defp command(["workspace-discard", id], opts) do
    revision = Keyword.get(opts, :revision)
    note = Keyword.get(opts, :note)

    if is_integer(revision) and revision > 0 and is_binary(note) and String.trim(note) != "" do
      {:ok,
       command_wire("workspaces.discard", %{"id" => id, "revision" => revision, "note" => note})}
    else
      {:error, :workspace_discard_requires_revision_and_note}
    end
  end

  defp command(["team-batches"], opts),
    do: {:ok, command_wire("children.list", %{"cursor" => Keyword.get(opts, :cursor, 0)})}

  defp command(["team-batch", key], _opts),
    do: {:ok, command_wire("children.get", %{"key" => key})}

  defp command(["team-result", key, child], opts) do
    generation = Keyword.get(opts, :generation)
    revision = Keyword.get(opts, :revision)
    cursor = Keyword.get(opts, :cursor, 0)

    cond do
      not (is_binary(generation) and String.trim(generation) != "") or
          not (is_integer(revision) and revision > 0) ->
        {:error, :team_result_requires_generation_and_revision}

      not (is_integer(cursor) and cursor >= 0) ->
        {:error, {:invalid_team_result, :cursor}}

      true ->
        {:ok,
         command_wire("children.result", %{
           "key" => key,
           "child" => child,
           "generation" => generation,
           "revision" => revision,
           "cursor" => cursor
         })}
    end
  end

  defp command(["team-child-approval", key, child], opts) do
    generation = Keyword.get(opts, :generation)
    revision = Keyword.get(opts, :revision)

    if nonempty?(generation) and positive?(revision) do
      {:ok,
       command_wire("children.approval", %{
         "key" => key,
         "child" => child,
         "generation" => generation,
         "revision" => revision
       })}
    else
      {:error, :team_child_approval_requires_generation_and_revision}
    end
  end

  defp command(["mailbox", root], opts),
    do:
      {:ok,
       command_wire("mailbox.list", %{"root" => root, "cursor" => Keyword.get(opts, :cursor, 0)})}

  defp command(["mailbox-compact"], _opts),
    do: {:ok, command_wire("mailbox.compact", %{})}

  defp command(["mailbox-get", root, key], _opts),
    do: {:ok, command_wire("mailbox.get", %{"root" => root, "key" => key})}

  defp command(["mailbox-cancel", root, key], _opts),
    do: {:ok, command_wire("mailbox.cancel", %{"root" => root, "key" => key})}

  defp command(["task", id], _opts), do: {:ok, command_wire("tasks.get", %{"id" => id})}

  defp command(["task-cancel", id], _opts),
    do: {:ok, command_wire("tasks.cancel", %{"id" => id})}

  defp command(["task-reconcile", id, resolution], opts)
       when resolution in ["committed", "failed", "retry"] do
    revision = Keyword.get(opts, :revision)
    note = Keyword.get(opts, :note)

    cond do
      not (is_integer(revision) and revision > 0) ->
        {:error, {:invalid_task_reconcile, :revision}}

      not (is_binary(note) and String.trim(note) != "") ->
        {:error, {:invalid_task_reconcile, :note}}

      true ->
        {:ok,
         command_wire("tasks.reconcile", %{
           "id" => id,
           "resolution" => resolution,
           "revision" => revision,
           "note" => note
         })}
    end
  end

  defp command(["task-decide", id, decision], opts) when decision in ["approve", "deny"] do
    revision = Keyword.get(opts, :revision)

    if is_integer(revision) and revision > 0 do
      {:ok,
       command_wire("tasks.decide", %{
         "id" => id,
         "revision" => revision,
         "decision" => decision
       })}
    else
      {:error, {:invalid_task_decide, :revision}}
    end
  end

  defp command(["task-decide", _id, _decision], _opts),
    do: {:error, {:invalid_task_decide, :decision}}

  defp command(["task-child-decide", task, child, decision], opts)
       when decision in ["approve", "deny"] do
    revision = Keyword.get(opts, :revision)
    batch_revision = Keyword.get(opts, :batch_revision)
    generation = Keyword.get(opts, :generation)
    attempt = Keyword.get(opts, :attempt)
    suspension = Keyword.get(opts, :suspension)
    key = Keyword.get(opts, :key)

    if positive?(revision) and positive?(batch_revision) and nonempty?(generation) and
         nonempty?(attempt) and nonempty?(suspension) and nonempty?(key) do
      {:ok,
       command_wire("tasks.child_decide", %{
         "id" => task,
         "child" => child,
         "decision" => decision,
         "revision" => revision,
         "generation" => generation,
         "batch_revision" => batch_revision,
         "attempt" => attempt,
         "suspension" => suspension,
         "key" => key
       })}
    else
      {:error, :invalid_task_child_decide}
    end
  end

  defp command(["task-child-decide", _task, _child, _decision], _opts),
    do: {:error, {:invalid_task_child_decide, :decision}}

  defp command(["task-cleanup", task], opts) do
    revision = Keyword.get(opts, :revision)

    if positive?(revision),
      do: {:ok, command_wire("tasks.cleanup", %{"id" => task, "revision" => revision})},
      else: {:error, :task_cleanup_requires_revision}
  end

  defp command(["task-recover", id, key], opts) do
    revision = Keyword.get(opts, :revision)
    generation = Keyword.get(opts, :generation)
    continuation_revision = Keyword.get(opts, :continuation_revision)

    if is_binary(id) and String.trim(id) != "" and is_binary(key) and String.trim(key) != "" and
         is_integer(revision) and revision > 0 and is_binary(generation) and
         String.trim(generation) != "" and is_integer(continuation_revision) and
         continuation_revision > 0 do
      {:ok,
       command_wire("tasks.recover", %{
         "id" => id,
         "key" => key,
         "revision" => revision,
         "generation" => generation,
         "continuation_revision" => continuation_revision
       })}
    else
      {:error, :invalid_parent_recovery_request}
    end
  end

  defp command([decision, request], _) when decision in ["approve", "deny"],
    do:
      {:ok,
       %{
         "type" => "approval_response",
         "request_id" => request,
         "decision" => if(decision == "approve", do: "approve", else: %{"deny" => "user"})
       }}

  defp command(_, _), do: {:error, :invalid_command}

  defp command_wire(name, payload),
    do: %{"type" => "command", "name" => name, "payload" => payload}

  defp maybe_put(payload, _key, nil), do: payload
  defp maybe_put(payload, key, value), do: Map.put(payload, key, value)

  defp positive?(value), do: is_integer(value) and value > 0
  defp nonempty?(value), do: is_binary(value) and String.trim(value) != ""

  defp connect(opts) do
    path = Keyword.get(opts, :socket, Path.join(Config.default_state_dir(), "service.sock"))
    Client.connect(path)
  end

  defp watch(client) do
    case Client.next(client, 30_000) do
      {:ok, %{"type" => "result"} = event} ->
        print(event)

      {:ok, event} ->
        print(event)
        watch(client)

      {:error, :timeout} ->
        watch(client)

      {:error, reason} ->
        {:error, {:disconnected, reason}}
    end
  end

  defp print(value), do: value |> JSON.encode!() |> IO.puts()
end
